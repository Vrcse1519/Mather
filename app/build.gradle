/*
 * Copyright 2016 icasdri
 *
 * This file is part of Mather. The original source code for Mather can be
 * found at <https://github.com/icasdri/Mather>. See COPYING for licensing
 * details.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "org.icasdri.mather"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 204
        versionName '0.2.4'

        vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

String sha384sum(File file) {
    byte[] contents = java.nio.file.Files.readAllBytes(file.toPath());
    java.security.MessageDigest digest = java.security.MessageDigest.getInstance("SHA-384");
    byte[] hash = digest.digest(contents);
    return String.format("%032X", new BigInteger(1, hash));
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'

    compile 'com.afollestad.material-dialogs:core:0.9.0.2'

    File tmpMathjsTgz = File.createTempFile("matherBuildTmp", "mathjs_npm.tgz", file('.'))
    tmpMathjsTgz.deleteOnExit();

    ant.get(src: 'https://registry.npmjs.org/mathjs/-/mathjs-3.5.3.tgz',
            dest: tmpMathjsTgz)

    assert 'E57014556DB9E4B33F1703ABAD75EFFD33C90F14D806D0B0D81FF7896813998D9D2E424328EEF7C41288EA23DF699CFA'
            .equals(sha384sum(tmpMathjsTgz))

    tarTree(resources.gzip(tmpMathjsTgz)).matching {
        include 'package/dist/math.min.js'
    }.visit {FileVisitDetails v ->
        if (!v.isDirectory()) {
            v.copyTo(file('src/main/res/raw/math.js'))
            v.stopVisiting();
        }
    }
}

