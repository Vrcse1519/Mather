/*
 * Copyright 2016 icasdri
 *
 * This file is part of Mather. The original source code for Mather can be
 * found at <https://github.com/icasdri/Mather>. See COPYING for licensing
 * details.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "org.icasdri.mather"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 204
        versionName '0.2.4'

        vectorDrawables.generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

String sha384sum(File file) {
    byte[] contents = java.nio.file.Files.readAllBytes(file.toPath());
    java.security.MessageDigest digest = java.security.MessageDigest.getInstance("SHA-384");
    byte[] hash = digest.digest(contents);
    return String.format("%032X", new BigInteger(1, hash));
}

void retrieveMathjs(String verWithHash) {
    String[] v = verWithHash.split('::')

    File mathjsFile = file('src/main/res/raw/math.js');

    boolean need = true;
    if (mathjsFile.isFile()) mathjsFile.withReader { reader ->
        char[] arr = new char[500];
        reader.read(arr, 0, 500);
        need = !new String(arr).contains(String.format("version %s", v[0]))
    }

    if (need) {
        File tmpMathjsTgz = File.createTempFile("matherBuildTmp", "mathjs_npm.tgz", file('.'))

        ant.get(src: String.format('https://registry.npmjs.org/mathjs/-/mathjs-%s.tgz', v[0]),
                dest: tmpMathjsTgz)

        assert v[1].toUpperCase().equals(sha384sum(tmpMathjsTgz))

        tarTree(resources.gzip(tmpMathjsTgz)).matching {
            include 'package/dist/math.min.js'
        }.visit {FileVisitDetails f ->
            if (!f.isDirectory()) {
                f.copyTo(mathjsFile)
                f.stopVisiting();
            }
        }

        tmpMathjsTgz.delete();
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'

    compile 'com.afollestad.material-dialogs:core:0.9.0.2'

    retrieveMathjs('3.5.3::e57014556db9e4b33f1703abad75effd33c90f14d806d0b0d81ff7896813998d9d2e424328eef7c41288ea23df699cfa')
}

